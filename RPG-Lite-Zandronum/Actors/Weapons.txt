ACTOR RPGFist : Fist
{
	States
	{

	Fire:
		PUNG B 3
		TNT1 A 0 A_JumpIfInventory("PowerStrength", 1, "Berserked")
  
	Normal1:
		PUNG C 4 A_CustomPunch(ACS_NamedExecuteWithResult("DAMAGESCR",10,0,0), TRUE)
		Goto FireEnd
  
	Berserked:
		PUNG C 4 A_CustomPunch(ACS_NamedExecuteWithResult("DAMAGESCR",40,0,0), TRUE)
  
	FireEnd:
		PUNG D 4
		PUNG C 3
		PUNG B 4 A_ReFire
		Goto Ready
	}
}

ACTOR RPGPistol : Pistol Replaces Pistol
{
	States
	{
	Fire:
		PISG A 4
		PISG B 0 A_FireBullets (5.6, 0, 1, ACS_NamedExecuteWithResult("DAMAGESCR",5,0,0), "BulletPuff")
		TNT1 A 0 A_PlaySound("weapons/pistol", CHAN_WEAPON)
		PISG B 6 A_GunFlash
		PISG C 4
		PISG B 5 A_ReFire
		Goto Ready
	Flash:
		PISF A 7 Bright A_Light1
		Goto LightDone
		PISF A 7 Bright A_Light1
		Goto LightDone
	}
}

ACTOR RPGChainsaw : Chainsaw Replaces Chainsaw
{
	States
	{
	Fire:
		SAWG AB 4 A_Saw ("weapons/sawfull","weapons/sawhit",ACS_NamedExecuteWithResult("DAMAGESCR",2,0,0))
		SAWG B 0 A_ReFire
		Goto Ready
	}
}

ACTOR RPGShotgun : Shotgun Replaces Shotgun
{
	States
	{
	Fire:
		SHTG A 3
		TNT1 A 0 A_FireBullets (5.6, 0, 7,ACS_NamedExecuteWithResult("DAMAGESCR",5,0,0), "BulletPuff")
		TNT1 A 0 A_PlaySound ("weapons/shotgf", CHAN_WEAPON)
		SHTG A 7 A_GunFlash
		SHTG BC 5
		SHTG D 4
		SHTG CB 5
		SHTG A 3
		SHTG A 7 A_ReFire
		Goto Ready
	}
}

ACTOR RPGSuperShotgun : SuperShotgun Replaces SuperShotgun
{
	States
	{
	Fire:
		SHT2 A 3
		SHT2 A 0 A_FireBullets (11.2, 7.1, 20, ACS_NamedExecuteWithResult("DAMAGESCR",5,0,0), "BulletPuff")
		SHT2 A 0 A_PlaySound ("weapons/sshotf", CHAN_WEAPON)
		SHT2 A 7 A_GunFlash
		SHT2 B 7
		SHT2 C 7 A_CheckReload
		SHT2 D 7 A_OpenShotgun2
		SHT2 E 7
		SHT2 F 7 A_LoadShotgun2
		SHT2 G 6
		SHT2 H 6 A_CloseShotgun2
		SHT2 A 5 A_ReFire
		Goto Ready
	// unused states
		SHT2 B 7
		SHT2 A 3
		Goto Deselect
	}
}

ACTOR RPGChaingun : Chaingun Replaces Chaingun
{
	States
	{
	Fire:
		CHGG A 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
		CHGG A 0 A_GunFlash
		CHGG A 4 A_FireBullets(5.6, 0, 1, ACS_NamedExecuteWithResult("DAMAGESCR",5,0,0), "BulletPuff")
		CHGG B 0 A_PlaySound("weapons/chngun", CHAN_WEAPON)
		CHGG B 0 A_GunFlash("Flash2")
		CHGG B 4 A_FireBullets(5.6, 0, 1, ACS_NamedExecuteWithResult("DAMAGESCR",5,0,0), "BulletPuff")
		CHGG B 0 A_ReFire
		Goto Ready
	Flash2:
		CHGF B 4 Bright A_Light2
		Goto LightDone
	}
}

ACTOR RPGRocketLauncher : RocketLauncher Replaces RocketLauncher
{
	States
	{
	Fire:
		MISG B 8 A_GunFlash
		MISG B 12 A_FireCustomMissile ("RPGRocket")
		MISG B 0 A_ReFire
		Goto Ready
	}
}

ACTOR RPGRocket : Rocket
{
	Damage (ACS_NamedExecuteWithResult("DAMAGESCR",20,1) * Random(1, 8))
}

ACTOR RPGPlasmaRifle : PlasmaRifle Replaces PlasmaRifle
{
	States
	{
	Fire:
		PLSG A 3 A_FireCustomMissile("RPGPlasmaBall")
		TNT1 A 0 A_GunFlash
		PLSG B 20 A_ReFire
		Goto Ready
	Flash:
		TNT1 A 0 A_Jump(127,2)
		PLSF A 4 Bright A_Light1
		Goto LightDone
		PLSF B 4 Bright A_Light1
		Goto LightDone
	}
}

ACTOR RPGPlasmaBall : PlasmaBall
{
	Damage (ACS_NamedExecuteWithResult("DAMAGESCR",5,1) * Random(1, 8))
}

ACTOR RPGBFG9000 : BFG9000 Replaces BFG9000
{
	States
	{
	Fire:
		BFGG A 20 A_BFGsound
		BFGG B 10 A_GunFlash
		BFGG B 10 A_FireCustomMissile("RPGBFGBall", 0, 1)
		BFGG B 20 A_ReFire
		Goto Ready
	}
}

ACTOR RPGBFGBall : BFGBall
{
	Damage (ACS_NamedExecuteWithResult("DAMAGESCR",100,1) * Random(1, 8))
}